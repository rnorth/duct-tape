import com.github.benmanes.gradle.versions.updates.DependencyUpdatesTask
import java.awt.*

buildscript {
    apply from: "gradle/buildscript.gradle", to: buildscript
}

group = 'org.rnorth.circuitbreakers'
description = 'Circuit Breakers pattern implementation for Java 8'

// convention: project.name == github project name == bintray package page
project.ext {
    build = {
        gradle = '2.2.1'
        java = 1.8
        // if not empty runs animal sniffer on compile to guarantee binary compatibility; set '' to disable
        //        signature = 'org.codehaus.mojo.signature:java16-sun:+@signature'
        signature = ''
    }
    github = {
        user = 'rnorth'
    }
    bintray = {
        user = project.hasProperty('bintrayUser') ? bintrayUser : 'USER' // configured in ~/.gradle/gradle.properties
        key = project.hasProperty('bintrayKey') ? bintrayKey : 'KEY' // configured in ~/.gradle/gradle.properties
        repo = 'maven'
        tags = []
        dryRun = false // whether to run this as dry-run, without deploying (TEST RUN)
        publish = true //If version should be auto published after an upload

        signFiles = true // true to gpg sign files on bintray (certificate must be installed on bintray account)
        gpgPassphrase = project.hasProperty('gpgPassphrase') ? gpgPassphrase : '' // optionally configured in ~/.gradle/gradle.properties if certificate requires it

        mavenCentralSync = true // true to sync with maven central (files signing required)
        ossUser = project.hasProperty('sonatypeUser') ? sonatypeUser : 'USER' // configured in ~/.gradle/gradle.properties
        ossPassword = project.hasProperty('sonatypePassword') ? sonatypePassword : 'PASSWORD' // configured in ~/.gradle/gradle.properties
    }
    license = 'MIT'
    licenseDesc = 'The MIT License'
    // extend pom for maven central acceptance
    pomConfig = {
        scm {
            url github.scm
            connection github.scmConnection
            developerConnection github.scmConnection
        }
        licenses {
            license {
                name licenseDesc
                url github.licenseUrl
                distribution "repo"
            }
        }
        developers {
            developer {
                id "rnorth"
                name "Richard North"
                email "rich.north@gmail.com"
            }
        }
    }
}

apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'org.asciidoctor.convert'
apply plugin: 'project-report'

apply from: 'gradle/build.gradle'
apply from: 'gradle/github.gradle'
apply from: 'gradle/bintray.gradle'
apply from: 'gradle/release.gradle'
apply from: 'gradle/maven.gradle'
apply from: 'gradle/quality.gradle'
apply from: 'gradle/asciidoclet.gradle'


repositories {
    jcenter()
    mavenCentral()
    mavenLocal()
}

dependencies {
    provided 'com.google.code.findbugs:jsr305:3.0.0'

    compile 'org.slf4j:slf4j-api:1.7.7'

    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-all:1.9.5'
    testCompile 'org.slf4j:slf4j-simple:1.7.7'
}

task checkOutOfDate(type: DependencyUpdatesTask, description: 'Checks dependencies updates availability') {
    revision 'release'
}

task showDependenciesTree(dependsOn: 'htmlDependencyReport', description:
        'Generates dependencies tree report and opens it in browser (useful to analyze dependencies)') << {
    Desktop.getDesktop().open(file('/build/reports/project/dependencies/root.html'))
}

task install(dependsOn: 'publishMavenJavaPublicationToMavenLocal', description: 'Install to local maven repository') << {
    logger.warn "INSTALLED $project.group:$project.name:$project.version"
}

